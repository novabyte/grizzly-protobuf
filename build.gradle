/**
 * Grizzly NIO framework filters to process Google's Protocol Buffers serialized
 * messages.
 */

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath(
            [group: 'ws.antonov.gradle.plugins', name: 'gradle-plugin-protobuf', version: '0.8']
        )
    }
}

apply plugin: 'java'
apply plugin: 'protobuf'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'eclipse'

group   = 'me.cmoz.grizzly'
version = '1.0.1-SNAPSHOT'

def groovyVersion = 'groovy-2.1.6'
def isReleaseVersion = !version.endsWith('SNAPSHOT')

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    tasks.withType(Compile).all {
        // enable all warnings as errors
        options.compilerArgs = [
            '-Xlint:cast,deprecation,divzero,empty,unchecked,fallthrough,path,serial,finally,overrides,-options',
            '-Werror',
            '-XprintProcessorInfo'
        ]
        options.encoding = "UTF-8"
    }
}

repositories {
    mavenCentral()
}

configurations {
    deployerJars
    provided
    testProvided { extendsFrom provided }
}

sourceSets {
    main { compileClasspath += configurations.provided }
    test { compileClasspath += configurations.testProvided }
}

dependencies {
    def slf4jVersion = '1.7.5'

    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-ssh', version: '2.4'

    provided group: 'org.projectlombok', name: 'lombok', version: '0.12.0'
    compile(
        [group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion],
        [group: 'org.glassfish.grizzly', name: 'grizzly-framework', version: '2.3.5'],
        [group: 'com.google.protobuf', name: 'protobuf-java', version: '2.5.0']
    )
    testCompile(
        [group: 'junit', name: 'junit', version: '4.11'],
        [group: 'org.mockito', name: 'mockito-core', version: '1.9.5']
    )
    testRuntime(
        [group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion]
    )
}

test {
    forkEvery 100
    jvmArgs '-Xms128m', '-Xmx512m', '-XX:MaxPermSize=1024m', '-enableassertions', '-Djava.awt.headless=true'
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Created-By': 'Gradle ' + gradle.gradleVersion,
                   'Build-Jdk': System.properties['java.version'],
                   'Manifest-Version': 1.0
    }
}

javadoc {
    classpath += configurations.provided
}

task sourceJar(type: Jar, dependsOn: classes) {
    baseName = project.name
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = project.name
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    if (isReleaseVersion) {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment ->
                signing.signPom(deployment)
            }

            if (isReleaseVersion) {
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            } else {
                repository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.project {
                groupId project.group
                artifactId project.name
                version project.version
                packaging 'jar'
                name 'Grizzly-Protobuf'
                description "Grizzly NIO framework filters to process Google's Protocol Buffers serialized messages."
                url 'http://github.com/novabyte/grizzly-protobuf'
                inceptionYear '2013'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/novabyte/grizzly-protobuf'
                    connection 'scm:git://github.com/novabyte/grizzly-protobuf.git'
                    developerConnection 'scm:git@github.com:novabyte/grizzly-protobuf.git'
                }
                developers {
                    developer {
                        id 'novabyte'
                        name 'Chris Molozian'
                        email 'chris@cmoz.me'
                    }
                }
            }
        }
    }
}
uploadArchives.dependsOn ':build'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        scopes.PROVIDED.plus += configurations.provided

        // enrich the IDE with Groovy support
        iml.withXml { provider ->
            provider.node.component.first().appendNode('orderEntry',
                    [type: 'library', name: groovyVersion, level: 'application'])
        }
    }
    project.ipr {
        // let the IDE know that this is a Git project
        withXml { provider ->
            provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
        }
    }
}

eclipse {
    classpath { plusConfigurations += configurations.provided }
}
